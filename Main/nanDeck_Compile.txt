UNIT = INCH
;Compile card builder
;by NZ3R0K
;Last Updated December 15, 2025

;USEAGE
;This program is designed to take in a .xlsx containing information about the Protocol and its Command Cards.
;Additionally some parameters may be altered in this file to change what generates, and from what source images are taken from.

;===========================================================================================================



;Parameters





;Only insert the root folder path, the subfolders will be made automatically.
[TTSFileSaveLocation] = "Generated\\"

;Only insert the root folder path, the subfolders will be made automatically.
[SpriteSheetSaveLocation] = "Generated\\"

;This folder contains the default / universal image components for Compile
[CompileImageDefaultsLocation] = "Universal Assets\\"










;----------------------------------------------------------------------------------------------------------
;XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
;XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
;XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
;----------------------------------------------------------------------------------------------------------


;CODE - Do not touch below this point! Unless you know what your doing :D


;----------------------------------------------------------------------------------------------------------
;XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
;XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
;XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
;----------------------------------------------------------------------------------------------------------










[card-w]=2.5
[card-h]=3.5
CARDSIZE=[card-w],[card-h]

INPUTNUMBER = TextBackgroundAlpha, “Transparency for command background. %”,75,0,100
INPUTTEXT = Path, “Select the xlsx file.”,, F
INPUTCHOICE = SliceImageForBackground, "Generate command background?","False","True|False"
INPUTCHOICE = OneImageCards, "Generate protocol background","False","True|False"
INPUTCHOICE = BuildTTSSheet, "Generate TTS Sheet?","False","True|False"
INPUTCHOICE = BuildSpriteSheet, "Generate generic sprite sheet?","False","True|False"
INPUTCHOICE = PrintToPage, "Format for printer paper?","False","True|False"

[TTSSheetSize] = 3x2
[SpriteSheetSize] = 3x3


;Dimensions for how many command cards per row and col on the given background image
[commandcards-x]=3
[commandcards-y]=2


;Dimensions for how many protocol cards per row and col on the given background image.
[protocolcards-x]=2
[protocolcards-y]=1

[ind]=index("\",[Path],-1)
[Root]=STRINGSUB([Path], 0, [ind])

LINK = "[Path]!Protocol"
LINK = "[Path]!Command"


;===========================================================================================================
;File Pathing
;===========================================================================================================


[ProtocolIcon] = "[Root]\Assets\\icon.png"

;Command Cards
[CommandCardTextBackground] = CONCAT([CompileImageDefaultsLocation],1,"CompileCards_DefaultTextBackground.png",1)
[CommandCardPersistentTextBackground] = CONCAT([CompileImageDefaultsLocation],1,"CompileCards_PersistentTextBackground.png",1)
[CommandCardImmediateTextBackground] = CONCAT([CompileImageDefaultsLocation],1,"CompileCards_ImmediateTextBackground.png",1)
[CommandCardAuxiliaryTextBackground] = CONCAT([CompileImageDefaultsLocation],1,"CompileCards_AuxiliaryTextBackground.png",1)
[CommandCardLines] = CONCAT([CompileImageDefaultsLocation],1,"CompileCards_Lines.png",1)

;Protocol Cards
[ProtocolCardLoadingTextBackground] = CONCAT([CompileImageDefaultsLocation],1,"ProtocolLoadingTextBackground.png",1)
[ProtocolCardLoadingLines] = CONCAT([CompileImageDefaultsLocation],1,"ProtocolLoadingLines.png",1)
[ProtocolCardCompiledTextBackground] = CONCAT([CompileImageDefaultsLocation],1,"ProtocolCompiledTextBackground.png",1)
[ProtocolCardCompiledLines] = CONCAT([CompileImageDefaultsLocation],1,"ProtocolCompiledLines.png",1)

;Other
[CardBackImage] = CONCAT([CompileImageDefaultsLocation],1,"Cardback.jpg",1)

[commandCardSheetDimensions] = [commandcards-x]|[commandcards-y]
[protocolCardSheetDimensions] = [protocolcards-x]|[protocolcards-y]


CARDS={[commandcards-x]*[commandcards-y]}+3


;===========================================================================================================
;Command Cards
;===========================================================================================================


;Generate background images
IF = ([SliceImageForBackground] = "True") _AND_ ({§} = 1)
log=,,"Beginning Image Slice..."
x=IMAGECREATE("ImageSlice.txt", "commandCard", [Root], [commandcards-x], [commandcards-y])
log=,,"Slice Complete"
ENDIF

IF = ([OneImageCards] = "True") _AND_ ({§} = 1)
log=,,"Beginning Image Glitch..."
x=IMAGECREATE("ImageGlitch.txt", [Root])

FOR = A, 1, 20, 1
save=,"frame{A}.jpg",0,0,1%,1%,,,,,,,F
NEXT

log=,,"Glitch Complete"
ENDIF

;Background Image(locked)
IMAGE=1-{[commandcards-x]*[commandcards-y]},"[Root]\Assets\\Generated\\commandCard{§}.jpg",0,0,[card-w],[card-h]
;DefaultTextBackground
IMAGE=1-6,[CommandCardTextBackground],0%,0%,100%,100%,0,N,[TextBackgroundAlpha]%
;Icon
IMAGE=1-6,[ProtocolIcon],79.5%,4.60%,14%,10%

;PersistentTextBackground
if = _NOT_ ([PERSISTENT TEXT] = )
IMAGE=1-6,[CommandCardPersistentTextBackground],0%,0%,100%,100%,0,,[TextBackgroundAlpha]%
endif

;ImmediateTextBackground
if = _NOT_ ([IMMEDIATE TEXT] = )
IMAGE=1-6,[CommandCardImmediateTextBackground],0%,0%,100%,100%,0,,[TextBackgroundAlpha]%
endif

;AuxiliaryTextBackground
if = _NOT_ ([AUXILIARY TEXT] = )
IMAGE=1-6,[CommandCardAuxiliaryTextBackground],0%,0%,100%,100%,0,,[TextBackgroundAlpha]%
endif


;Lines
IMAGE=1-6,[CommandCardLines],0%,0%,100%,100%,0,NAT
;ProtocolNameFont
HTMLFONT=ProtocolNameFont,HACKED,20,,#FFFFFF,LEFT
;AbilityFont
HTMLFONT=AbilityFont,TTSupermolot-Regular,9,,#FFFFFF,LEFT,0,0,0,,,0,0,,0.0025
HTMLFONT=AbilityFontEmphasis,TTSupermolot-Bold,9,UZ,#FFFFFF,,0,0,0,,,0,0,,0.0025
;PackFont
HTMLFONT=PackTagFont,TTSupermolot-Regular,6,ZW,#FFFFFF,RIGHT,0.005,0.005,0.1,#000000
;ValueFont
HTMLFONT=ValueFont,HACKED,36,,#FFFFFF,CENTER
;CreatorFont
HTMLFONT=CreatorTagFont,TTSupermolot-Regular,6,Z,#FFFFFF,LEFT,0.005,0.005,0.1,#000000
;CardNameFont
HTMLFONT=CardNameFont,TTSupermolot-Regular,5,Z,#FFFFFF,LEFT,0.005,0.005,0.1,#000000
;VersionFont
HTMLFONT=VersionFont,TTSupermolot-Regular,4,Z,#FFFFFF,RIGHT,0.005,0.005,0.1,#000000

;AbilityMargins
HTMLMARGINS=AbilityFont,0,0.15,0,0,CENTER,125
;ValueMargins
HTMLMARGINS=ValueFont,0,0.1,0.1,0,CENTER,120
;CommandCardProtocolMargins
HTMLMARGINS=ProtocolNameFont,0,0,0,0,CENTER

;ValueText
HTMLTEXT=1-6,[VALUE],{23372/571}%,{1097/160}%,{10658/571}%,{5257/400}%,#FFFFFF,0,EB,100,ValueFont,ValueFont
;Pack Tag
HTMLTEXT=1-6,[PACK],91.75%,46.471%,3.75%,18.25%,#FFFFFF,-90,BE,100,PackTagFont

if= _NOT_ ([CARD NAME] = )
HTMLTEXT=1-6,[PROTOCOL NAME],{4187/571}%,4%,30%,{619/100}%,#FFFFFF,0,EFB,100,ProtocolNameFont,ProtocolNameFont
HTMLTEXT=1-6,[CARD NAME],8%,8.25%,25.732%,3.24%,#FFFFFF,0,BE,100,CardNameFont
else
HTMLTEXT=1-6,[PROTOCOL NAME],{4187/571}%,{3809/800}%,30%,{619/100}%,#FFFFFF,0,EFB,100,ProtocolNameFont,ProtocolNameFont
endif

[PERSTEXT] = REPLACE([PERSISTENT TEXT],\13\,"<br>")
[IMMTEXT] = REPLACE([IMMEDIATE TEXT],\13\,"<br>")
[AUXTEXT] = REPLACE([AUXILIARY TEXT],\13\,"<br>")

[PEREMPH] = REPLACE([PERSISTENT EMPHASIS],\13\,"<br>")
[IMMEMPH] = REPLACE([IMMEDIATE EMPHASIS],\13\,"<br>")
[AUXEMPH] = REPLACE([AUXILIARY EMPHASIS],\13\,"<br>")

;If there is an emphasis, use different concat
if = _NOT_ ([PERSISTENT EMPHASIS] = )
HTMLKEY = 1-6, (emphasis), "{PEREMPH?§}:", AbilityFontEmphasis
HTMLTEXT=1-6,"(emphasis) {PERSTEXT?§}",4.5%,{18209/800}%,88%,{3459/160}%,#FFFFFF,0,BE,100,AbilityFont
else
HTMLTEXT=1-6,{PERSTEXT?§},4.5%,{18209/800}%,88%,{3459/160}%,#FFFFFF,0,BE,100,AbilityFont
endif

;If there is an emphasis, use different concat
if =_NOT_ ([IMMEDIATE EMPHASIS] = )
HTMLKEY = 1-6, (emphasis), "{IMMEMPH?§}:", AbilityFontEmphasis
HTMLTEXT=1-6,"(emphasis) {IMMTEXT?§}",4.5%,{38933/800}%,{48116/571}%,{8533/400}%,#FFFFFF,0,BE,100,AbilityFont
else
HTMLTEXT=1-6,{IMMTEXT?§},4.5%,{38933/800}%,{48116/571}%,{8533/400}%,#FFFFFF,0,BE,100,AbilityFont
endif

;If there is an emphasis, use different concat
if =_NOT_ ([AUXILIARY EMPHASIS] = )
HTMLKEY = 1-6, (emphasis), "{AUXEMPH?§}:", AbilityFontEmphasis
HTMLTEXT=1-6,"(emphasis) {AUXTEXT?§}",4.5%,72%,85%,24%,#FFFFFF,0,BE,100,AbilityFont
else
HTMLTEXT=1-6,{AUXTEXT?§},4.5%,72%,85%,24%,#FFFFFF,0,BE,100,AbilityFont
endif

;CreatorTag
HTMLTEXT=1-6,[CREATOR TAG],{29082/571}%,{3779/40}%,{24058/571}%,{259/80}%,#FFFFFF,0,BE,100,CreatorTagFont
;VersionTag
HTMLTEXT=1-6,[VERSION],{23525/571}%,{819/200}%,{1598/571}%,{12419/800}%,#FFFFFF,90,BE,100,VersionFont

;===========================================================================================================
;Protocol Card - Loading Side
;===========================================================================================================


;ProtocolCard Loading Background(locked)
IMAGE=7,"[Root]\Assets\\Generated\\ProtocolFrontImage.jpg",0,0,[card-w],[card-h]
;Lines(snap)(size)
IMAGE=7,[ProtocolCardLoadingLines],0%,0%,100%,100%
;Image(snap)(size)
IMAGE=7,[ProtocolCardLoadingTextBackground],0%,0%,100%,100%,0,,[TextBackgroundAlpha]%
;Icon
IMAGE=7,[ProtocolIcon],79%,84.55%,{7993/571}%,10%,90
;PCProtocolFont
HTMLFONT=PCProtocolFont,HACKED,38,,#FFFFFF,CENTER,0,0,0.25,#000000
;PCDescriptionFont(size)
HTMLFONT=PCDescriptionFont,TTSupermolot-Regular,8,W,#FFFFFF,LEFT
;PCActionFont(size)
HTMLFONT=PCActionFont,TTSupermolot-Regular,8,W,#FFFFFF,CENTER
;ProtocolMargins
HTMLMARGINS=PCProtocolFont,0,0,0,0,CENTER,0
;ProtocolActionMargin
HTMLMARGINS=PCActionFont,0,0,0,0,CENTER
;ProtocolText(size)
HTMLTEXT=7,[PROTOCOL NAME],{22763/571}%,{259/80}%,22%,96%,#FFFFFF,90,EB,100,PCProtocolFont
;PCLoadingFont
HTMLFONT=PCLoadingFont,Motion Control,16,W,#FFFFFF,CENTER,0,0,0.25,#000000
;LoadingText
HTMLTEXT=7,"Loading...",{17282/571}%,{31009/800}%,{4415/571}%,{20419/800}%,#FFFFFF,90,EB,100,PCLoadingFont
;DescriptionText(size)
HTMLTEXT=7,"> [PROTOCOL DESCRIPTION]",87.5%,{3123/400}%,6%,70%,#FFFFFF,90,EB,100,PCDescriptionFont
;PCActionText(size)
HTMLTEXT=7,[PROTOCOL ACTIONS],8.5%,6.5%,8%,86%,#FFFFFF,90,EB,100,PCActionFont


;===========================================================================================================
;Protocol Card - Compiled Side
;===========================================================================================================


;ProtocolCard Compiled Background(locked)
IMAGE=8,"[Root]\Assets\\Generated\\ProtocolBackImage.jpg",0,0,[card-w],[card-h]
;TextBackground(snap)(size)
IMAGE=8,[ProtocolCardCompiledTextBackground],0%,0%,100%,100%,0,,[TextBackgroundAlpha]%
;Lines(snap)(size)
IMAGE=8,[ProtocolCardCompiledLines],0%,0%,100%,100%
;ProtocolCompiledProtocolFont(snap)(size)
HTMLFONT=ProtocolCompiledProtocolFont,HACKED,24,W,#FFFFFF,CENTER
;CompiledFont(snap)(size)
HTMLFONT=CompiledFont,Bahnschrift,10,W,#FFFFFF,CENTER
;CompiledMargins(snap)(size)
HTMLMARGINS=CompiledFont,0,0,0,0,CENTER
;CompiledText(snap)(size)
HTMLTEXT=8,"Compiled",6%,64%,8%,30%,#FFFFFF,90,EB,100,CompiledFont
;ProtocolText(size)
HTMLTEXT=8,[PROTOCOL NAME],{3806/571}%,{2933/400}%,14%,52%,#FFFFFF,90,EB,100,ProtocolCompiledProtocolFont
;Icon(size)
IMAGE=8,[ProtocolIcon],80.25%,84.75%,{7993/571}%,10%,90

;===========================================================================================================
;Card Back
;===========================================================================================================


image=9,[CardBackImage],0,0,[card-w],[card-h]


;===========================================================================================================
;File Generation
;===========================================================================================================



IF = [BuildSpriteSheet] = "True"
COPYCARD="10-11","7-8"
COPYCARD="12","9"
COPYCARD="13-18","1-6"
DISPLAY=CONCAT([SpriteSheetSaveLocation],1,[PROTOCOL NAME],1,"_SpriteSheet.png",1),,,[SpriteSheetSize],10-18
ENDIF

IF = [BuildTTSSheet] = "True"
DISPLAY=CONCAT([TTSFileSaveLocation],1,[PROTOCOL NAME],1,"\\CommandSheet.png",1),,,[TTSSheetSize],1-6
DISPLAY=CONCAT([TTSFileSaveLocation],1,[PROTOCOL NAME],1,"\\A.png",1),,,1x1,7
DISPLAY=CONCAT([TTSFileSaveLocation],1,[PROTOCOL NAME],1,"\\B.png",1),,,1x1,8
ENDIF

IF = [PrintToPage] = "True"
DUPLEX = "7","8"
DUPLEX = "1-6","9"
GAP=0.175,0.275,ON,OFF,DASHED
MARGINS=0.175,0.175,0.275,0.275
PRINT = DUPLEX
ENDIF